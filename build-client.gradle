
// static
static def getDate() {
    return new Date().format('dd.MM.yyyy')
}
def currentDate = getDate()
def DIR_CSS = project.file("src/main/resources/static/css/dev")
def DIR_JS = project.file("src/main/resources/static/js/modules")
def dirClient = "$buildDir/client"
def dirMin = "$buildDir/min"
// from gradle.properties
def dirTomcat = "${tomcatHome}/webapps/${appName}"

tasks.register('cclean') {
    doFirst {
        println "clean: build directory"
        file("$buildDir").list().each {
            f -> delete "$buildDir/${f}"
        }
    }
}

tasks.register('client') {
    doFirst {
        println "Create static JS client..."

        copy {
            println "copy: json files"
            from "src/main/resources/json/output"
            into "$buildDir/client/json"
            include('**/*.json')
        }
        copy {
            println "copy: tailwindcss"
            from "src/main/resources/static/css/external/tailwind.min.form.css"
            into "$buildDir/client/node_modules/tailwindcss/dist"
        }
        copy {
            println "copy: static"
            println "copy: assets"
            from "src/main/resources/static"
            exclude ('**/js', '**/css') // '**/temp', '**/tmp', '**/_tmp'
            println "copy: package.json"
            from layout.projectDirectory.file("package.json")
            println "copy: robots.txt"
            from layout.projectDirectory.file("robots.txt")
            println "copy: favicon.ico"
            from layout.projectDirectory.file("src/main/resources/static/assets/images/favicon.ico")
            into dirClient
        }
        copy {
            println "copy: html"
            from layout.projectDirectory.file("src/main/resources/templates/index.html")
            from layout.projectDirectory.file("src/main/resources/templates/static/login.html")
            filter { String line ->
                line.replaceAll('/users/login', "/login.html")
            }
            filter { String line ->
                line.contains('favicon.ico') ? "<link rel=\"icon\" href=\"favicon.ico?v=${currentDate}\">" : line
            }
            filter { String line ->
                line.contains('/main.css') ? "<link rel=\"stylesheet\" href=\"/css/style.css?v=${currentDate}\" type=\"text/css\">" : line
            }
            filteringCharset = 'UTF-8'
            into dirClient
        }
        copy {
            println "copy: style.css"
            from "$buildDir/min/css/style.min.css"
            into "$buildDir/client/css"
            rename("style.min.css", "style.css")
        }
        copy {
            println "copy: main.js"
            from layout.projectDirectory.file("src/main/resources/static/js/main.js")
            into "$buildDir/client/js"
            filter { String line ->
                line.replaceAll("./modules/factory.js", "./modules/modules.js")
            }
        }
        copy {
            println "copy: maps"
            from "src/main/resources/static/js/modules/components/maps"
            into "$buildDir/client/js/modules/components/maps"
        }
    }
    doLast {
        project.sync {
            println "sync to tomcat/webapps/${appName}"
            from dirClient
            into dirTomcat
        }
    }
}

task concatCSS(type: ConcatTask) {
    files = files(
            project.file("$DIR_CSS/main.css"),
            project.file("$DIR_CSS/generic-normalize.css"),
            project.file("$DIR_CSS/style.css"),
            project.file("$DIR_CSS/responsive.css"),
            project.file("$DIR_CSS/quiz.css"),
            project.file("$DIR_CSS/development.css")
    )
    target = project.file("$buildDir/concat/css/style.css")
    filters = ["@import"]
    finalizedBy('CssMinify')
}

task concatJS(type: DefaultTask) {
    doFirst {
        copy {
            println "copy: constants.js"
            from project.file("$DIR_JS/constants.js")
            filter { String line ->
                line.startsWith('version:') ? "version: \"${currentDate}\"," : line
            }
            filter { String line ->
                line.startsWith('isStatic:') ? 'isStatic: true,' : line
            }
            filteringCharset = 'UTF-8'
            println "copy: factory.js"
            from project.file("$DIR_JS/factory.js")
            filter { String line ->
                line.startsWith('    factory.addClass(') ? '' : line
            }
            into layout.buildDirectory.dir('concat/tmp')
        }
        copy {
            println "copy: maps.js"
            from project.file("$DIR_JS/controllers/maps.js")
            filter { String line ->
                line.replaceAll('../components/maps/', './components/maps/')
            }
            from project.file("$DIR_JS/factory_suffix.js")
            into layout.buildDirectory.dir('concat/tmp')
        }
    }
    finalizedBy('concatJS_files')
}

task concatJS_files(type: ConcatTask) {
    files = files(
            // prefixes
            project.file("$buildDir/concat/tmp/constants.js"),
            // utils
            project.file("$DIR_JS/utils/dom.js"),
            project.file("$DIR_JS/utils/counters.js"),
            project.file("$DIR_JS/utils/flags.js"),
            project.file("$DIR_JS/utils/numbers.js"),
            project.file("$DIR_JS/utils/objects.js"),
            project.file("$DIR_JS/utils/strings.js"),
            // modules
            project.file("$DIR_JS/data.js"),
            project.file("$DIR_JS/factory_loader.js"),
            // routes
            project.file("$DIR_JS/routes/routes.js"),
            project.file("$DIR_JS/routes/router.js"),
            project.file("$DIR_JS/routes/page.js"),
            // controllers
            project.file("$DIR_JS/controllers/exercise.js"),
            project.file("$DIR_JS/controllers/flashcards.js"),
            project.file("$DIR_JS/controllers/quizzes.js"),
//            project.file("$DIR_JS/controllers/maps.js")   todo: minify + dynamic import ?
            // components
            project.file("$DIR_JS/components/components.js"),
            project.file("$DIR_JS/components/notify.js"),
            project.file("$DIR_JS/components/bar.js"),
            project.file("$DIR_JS/components/cards.js"),
            project.file("$DIR_JS/components/stats.js"),
            project.file("$DIR_JS/components/input.js"),
            project.file("$DIR_JS/components/list.js"),
            project.file("$DIR_JS/components/navigation.js"),
            project.file("$DIR_JS/components/topics.js"),
            // suffixes
            project.file("$buildDir/concat/tmp/factory.js")
    )
    target = project.file("$buildDir/concat/js/modules.js")
    filters = ["import", "export"]
    finalizedBy('JsMinify')
//    finalizedBy('concatJS_finalize')
}

task concatJS_finalize(type: ConcatTask) {
    files = files(
//            project.file("$buildDir/concat/js/modules.js"),
            project.file("$buildDir/min/js/modules.min.js"),
            project.file("$buildDir/concat/tmp/maps.js"),
            project.file("$buildDir/concat/tmp/factory_suffix.js")
    )
    target = project.file("$buildDir/client/js/modules/modules.js")
    filters = ["import", "export"]
    doLast {
        target.append("const {data, page, topics, breadcrumb, router, navigation, notify} = await factory.init();")
        target.append("export {router}")
    }
}


class ConcatTask extends DefaultTask {
    @InputFiles
    ArrayList<String> filters
    @InputFiles
    FileCollection files
    @OutputFile
    File target
    @TaskAction
    void concat() {
        int count =  FilesUtil.concat(files, target, filters)
        println("ConcatTask: ${count} files to ${target.toString()}.")
    }
}

class FilesUtil {

    static File create(File file) {
        file.getParentFile().mkdirs()
        if (!file.exists()) {
            file.createNewFile()
        }
        return file
    }

    static boolean isValid(String line, ArrayList<String> filters) {
        def result = true;
        filters.each { str ->
            if (line.startsWith(str)) {
                result = false
            }
        }
        return result
    }

    static int concat(FileCollection files, File target, ArrayList<String> filters) {
        int i = 0;
        target.withWriter('UTF-8') { writer ->   // withPrintWriter
            files.each { file ->
                if (file.isFile()) {
                    file.eachLine { line ->
                        if (FilesUtil.isValid(line, filters)) {
                            writer.println(line)
                        }
                    }
                    i++
                }
            }
        }
        return i
    }

    static int extract(File source, File target, String start, String stop, boolean isOuter, ArrayList<String> filters) {
        int i = 0;
        boolean isExtract = isOuter;
        target.withWriter('UTF-8') { writer ->
            source.eachLine { line ->
                if (line.startsWith(start)) {
                    isExtract = !isExtract
                }
                if (isExtract && FilesUtil.isValid(line, filters)) {
                    writer.println(line)
                    i++
                }
                if ((isExtract != isOuter) && line.startsWith(stop)) {
                    isExtract = !isExtract
                }
            }
        }
        return i
    }
}

/**  example: concat class
 *
    class ConcatFiles extends DefaultTask {
        @InputFiles
        FileCollection files
        @OutputFile
        File target
        @TaskAction
        void concat() {
            target.withWriter { writer ->
                files.each { file ->
                    file.withReader { reader ->
                        writer << reader << '\n'
                    }
                }
            }
        }
    }
    task concat(type: ConcatFiles) {
        files = files(project.file("..."), project.file("..."))
        target = file("...")
    }

 **  example: concat all files in directory
 *
    task concatJS_Routes(type: ConcatTask) {
        files = files(project.file("$DIR_JS/routes").listFiles())
        target = project.file("$buildDir/concat/tmp/routes.js")
    }

 ** example: extract function from file
 *
    task extractGetName(type: ExtractFromFileTask) {
        source = project.file("$DIR_JS/my.js")
        target = project.file("$buildDir/getName.js")
        start = "function getName()"
        stop = "}"
        isOuter = false
    }

    class ExtractFromFileTask extends DefaultTask {
        String start
        String stop
        boolean isOuter
        ArrayList<String> filters
        @InputFiles
        File source
        @OutputFile
        File target
        @TaskAction
        void extract() {
            int count =  FilesUtil.extract(source, target, start, stop, isOuter, filters)
            println("ExtractFromFileTask: ${count} lines from ${source.toString()}.")
        }
    }
*/